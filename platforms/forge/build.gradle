// Build script for Terrain Control for Forge

buildscript
{
    repositories
    {
        mavenCentral()
        mavenLocal()
        jcenter() // For Shadow plugin
        maven
        {
            url = "http://files.minecraftforge.net/maven"
        }
        maven
        {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies
    {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
    }
}

apply plugin: 'shadow'
apply plugin: 'forge'

// Project properties
archivesBaseName = "terraincontrol-forge"
description = "TerrainControl for Minecraft Forge"
ext.ideProjectName = "TerrainControl-Forge"
minecraft.version = "1.7.2-10.12.0.1013"

repositories
{
    mavenCentral()
    mavenLocal()
}

dependencies
{
    compile project(':common')
    compile 'org.apache.logging.log4j:log4j-api:2.0-beta9'
    compile 'io.netty:netty-all:4.0.10.Final'
}

// Replace text in mcmod.info
processResources
{
    // Replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs)
    {
        include 'mcmod.info'

        // Replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // Copy everything else, that's not the mcmod.info
    from(sourceSets.main.resources.srcDirs)
    {
        exclude 'mcmod.info'
    }
}

// Include all Terrain Control files in the jar
shadow
{
    artifactAttached = false
    include 'com/khorn/terraincontrol/**'
}
