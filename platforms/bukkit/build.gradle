// Build script for Terrain Control for Bukkit

apply plugin: 'java'

// Project properties
archivesBaseName = "terraincontrol-bukkit"
description = "TerrainControl for Bukkit"
ext.ideProjectName = "TerrainControl-Bukkit"
ext.bukkitVersion = "1.8-R0.1-SNAPSHOT"

repositories
{
    mavenCentral() // For log4j
    mavenLocal()
}

dependencies
{
    testCompile 'junit:junit:4+'
    // You'll need to download Spigot Server and API manually
	// There are no public repos for these files
    // http://www.spigotmc.org/threads/bukkit-craftbukkit-spigot-1-8.36598/
    // The BuildTools.jar file will automatically install it in mavenLocal
    // so chances are it's already there if you have used BuildTools earlier
    compile 'org.spigotmc:spigot-api:' + project.ext.bukkitVersion
    compile 'org.spigotmc:spigot:' + project.ext.bukkitVersion
    compile project(':common')
    compile 'org.apache.logging.log4j:log4j-api:2.0-beta9'
    compile 'org.apache.logging.log4j:log4j-core:2.0-beta9'
}

// Replace text in plugin.yml
processResources
{
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version

    // Replace stuff in plugin.yml, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'

        // Replace version
        expand 'version': project.version
    }

    // Copy everything else, that's not the plugin.yml
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

// Include all Terrain Control files in the jar
jar
{
    destinationDir = new File(project.buildDir, "distributions")
    classifier = ''
    from
    {
        project(":common").sourceSets.main.output.files
    }
}
